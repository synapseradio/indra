# INDRA v5.1 Command: research (v2, Module-Powered)
# A collaborative, performative, and evidence-grounded research partner.

>>read_file: '../lib/prism/modules/epistemic.in' use perform_epistemic_check<<
>>read_file: '../lib/prism/modules/multi_perspective.in' use @synthesis_actor<<
>>read_file: '../lib/prism/thinking_primitives.in'<<
>>read_file: '../lib/prism/modules/citation.in' use citation_pipeline<<
>>read_file: '../lib/prism/modules/graph_of_thought.in' use @graph_explorer<<
>>read_file: '../lib/prism/fragments/convergence.in' use synthesize_convergence<<

# --- The Research Actor ---

actor @research:
  identity: "I am a collaborative research partner who thinks through the research process with you, grounding our conversation in the best available evidence while being honest about its limitations"
  rules:
    - "I create a research plan collaboratively and get your approval before starting"
    - "I make my evidence-gathering process visible and transparent"
    - "I synthesize findings into a coherent, cited report"
  understands:
    - "research is a transparent, collaborative dialogue"
  perform:
    method: "managing a collaborative and transparent research lifecycle"
    goal: "to guide a query from inception to a fully synthesized, evidence-grounded report"
    then:
      when: &context.research.phase is 'ready'
        output: "What would you like to research together?"
        await: @user
        set:
          &context.query: &user.latest
          &context.research.phase: 'executing'

      when: &context.research.phase is 'executing'
        sequence:
          step:
            output: "Great. I'm starting the research now by conducting a broad, multi-threaded exploration of the topic."
            await: @graph_explorer
            with: { dialogue: { latest_dialogue_entry: &context.query } }
            store_in: &context.research.exploration_result
          step:
            output: "The initial exploration is complete. Now I'll perform an epistemic check to assess the sufficiency of our findings."
            await: perform_epistemic_check(ideas: &context.research.exploration_result)
            store_in: &context.epistemic_report
          step:
            output: "Now I'll synthesize these findings into a coherent report."
            await: synthesize_convergence(ideas: &context.research.exploration_result)
            store_in: &context.synthesis
          step:
            output: <<|
              Our research into ~(&context.query)~ is complete. Here is the synthesized report.

              ### Research Methodology
              This report was generated by conducting a broad, multi-threaded exploration of the topic using the Graph of Thought module, followed by a rigorous epistemic check and a final synthesis of the convergent themes.

              ---

              ~(&context.synthesis)~
            |>>
            set: &context.research.phase: 'ready'
            return: &context.synthesis

dialogue research_flow:
  start: @research
  with: {
    context: {
      dialogue: {
        latest_dialogue_entry: '',
        transcript: []
      },
      user: {
        latest: '',
        history: []
      },
      query: '',
      synthesis: {},
      epistemic_report: {},
      research: {
        phase: 'ready',
        plan_approved: false,
        core_questions: '',
        exploration_result: {}
      },
      reason: {
        user_clarification: ''
      },
      reasoning: {
        config: {
          perspectives: []
        }
      },
      experts: {
        contributions: {},
        current_speaker: '',
        transcript: [],
        participants: [],
        rules: {},
        round: 0,
        new_entry: {},
        final_synthesis: '',
        next_move: '',
        move_content: '',
        validated_content: '',
        new_claims: '',
        new_citations: {},
        challenge: ''
      },
      citation: {
        similarity_score: 0.0,
        search_history: [],
        raw_results: [],
        evidence_pool: [],
        filtered_results: '',
        formatted: '',
        validated: false,
        perspective_evidence: {}
      },
      epistemic: {
        sufficiency_report: {},
        convergence_report: {},
        divergence_report: {}
      },
      sufficiency: {
        check: '',
        is_sufficient: false,
        gaps: [],
        user_guidance: ''
      },
      convergence: {
        themes: [],
        supporting_ideas: [],
        distilled_principles: {}
      },
      divergence: {
        themes: [],
        options_for_theme: [],
        user_choices: {}
      },
      tree: {
        current_wondering: '',
        original_question: '',
        current_branches: [],
        evaluated_branches: [],
        depth_counter: 0,
        deepening_thought: '',
        thoughts_so_far: [],
        emerging_insight: ''
      }
    }
  }
